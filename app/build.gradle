import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "org.xvdr.robotv"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName "0.3.0.${gitAbbreviatedId}"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            archivesBaseName = "robotv-" + gitBranch + "-" + gitAbbreviatedId
        }
        debug {
            archivesBaseName = "robotv"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    task ndkBuild(type: Exec) {
        def ndkDir = project.plugins.findPlugin('com.android.application').sdkHandler.getNdkFolder()
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine "$ndkDir/ndk-build.cmd", '-C', file("$projectDir").absolutePath
        } else {
            commandLine "$ndkDir/ndk-build", '-C', file("$projectDir").absolutePath
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    provided fileTree(include: ['*.so'], dir: 'libs')
    compile 'com.android.support:leanback-v17:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
}
